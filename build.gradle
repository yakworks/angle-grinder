buildscript {
  repositories {
    mavenLocal()
    maven { url "https://plugins.gradle.org/m2/" }
    maven { url "https://repo.grails.org/grails/core" }
    maven { url "http://repo.9ci.com/oss-snapshots" }
  }
  dependencies {
    classpath "org.grails:grails-gradle-plugin:$grailsVersion"
    classpath "org.yakworks:gradle-plugins:1.1.14"
  }
}

apply plugin: 'yakworks.shipyak' //our opinionated defaults for gradle groovy & grails projects


subprojects {
  plugins.withId('java') {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    dependencies {
      compile "org.grails.plugins:converters:3.3.1" //JSON converter
      compile "org.grails:grails-plugin-controllers"
      testRuntime "com.h2database:h2"
      testRuntime "org.apache.tomcat:tomcat-jdbc"

      testCompile "org.grails:grails-gorm-testing-support"
      testCompile "org.grails:grails-web-testing-support"
      testCompile "org.grails.plugins:build-test-data:$versionBuildTestData"
      testCompile "org.grails:views-json-testing-support:$versionViews"
    }
  }
  configurations {
    testCompile.extendsFrom compileOnly //includes the compileOnly in tests too so we don't have to list twice
    all {
      resolutionStrategy.cacheChangingModulesFor 1, 'seconds' //when changing = true this sets the cache time
    }
  }
}

ext.codenarcRuleset= '''
  //getRule('Println').enabled = false
'''
